DESCRIPTION = "Cherokee Web Server fast and secure"
DESCRIPTION_cget = "Small downloader based in the Cherokee client library"
HOMEPAGE = "http://www.cherokee-project.com/"
SECTION = "network"
LICENSE = "GPLv2"
LIC_FILES_CHKSUM = "file://COPYING;md5=94d55d512a9ba36caa9b7df079bae19f"

INC_PR = "r0"

DEPENDS = "libpcre openssl ${@base_contains('DISTRO_FEATURES', 'pam', 'libpam', '', d)}"

SRC_URI = "http://www.cherokee-project.com/download/1.2/${PV}/cherokee-${PV}.tar.gz \
           file://cherokee.init \
           file://cherokee.service"


inherit autotools pkgconfig binconfig update-rc.d

EXTRA_OECONF = "--disable-static \
                --disable-nls \
               ${@base_contains('DISTRO_FEATURES', 'pam', '--enable-pam', '--disable-pam', d)} \
               "

do_install_append () {
	install -m 0755 -d ${D}${sysconfdir}/init.d
	install -m 755 ${WORKDIR}/cherokee.init ${D}${sysconfdir}/init.d/cherokee

	install -d ${D}${base_libdir}/systemd/system
	install -m 0644 ${WORKDIR}/*.service ${D}${base_libdir}/systemd/system/

	# clean up .la files for plugins
	rm -f ${D}${libdir}/cherokee/*.la
}

# Put -dev near the front so we can move the .la files into it with a wildcard
PACKAGES =+ "libcherokee-server libcherokee-client libcherokee-base cget"

FILES_${PN} += "${base_libdir}/systemd"
FILES_cget = "${bindir}/cget"
FILES_libcherokee-server = "${libdir}/libcherokee-server${SOLIBS}"
FILES_libcherokee-client = "${libdir}/libcherokee-client${SOLIBS}"
FILES_libcherokee-base = "${libdir}/libcherokee-base${SOLIBS}"

CONFFILES_${PN} = " \
                   ${sysconfdir}/cherokee/cherokee.conf \
                   ${sysconfdir}/init.d/cherokee \
                  "

INITSCRIPT_NAME = "cherokee"
INITSCRIPT_PARAMS = "defaults 91 91"
